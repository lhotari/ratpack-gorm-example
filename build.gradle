buildscript {
  repositories {
    maven { url new File("../ratpack/build/localrepo").toURL() }
    jcenter()
  }
  dependencies {
    classpath "io.ratpack:ratpack-gradle:0.9.9-SNAPSHOT"
  }
}

apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: "idea"
apply plugin: "eclipse"

repositories {
  maven { url new File("../ratpack/build/localrepo").toURL() }
  jcenter()
  maven { url "http://repo.springsource.org/repo" } // for springloaded
}

dependencies {
  compile "io.ratpack:ratpack-spring-boot:0.9.9-SNAPSHOT"
  compile 'org.grails:gorm-hibernate4-spring-boot:1.1.0.RELEASE'
  compile 'org.grails:grails-core:2.4.3'
  compile 'org.grails:grails-async:2.4.3'
  compile 'org.grails:grails-validation:2.4.3'
  compile 'org.springframework:spring-orm:4.0.6.RELEASE'
  def springBootVersion = '1.1.5.RELEASE'
  compile "org.springframework.boot:spring-boot-cli:$springBootVersion", {
     exclude group:'org.codehaus.groovy', module:'groovy'
  }
  compile 'org.codehaus.groovy:groovy-all:2.3.6'
  compile "org.springframework.boot:spring-boot-autoconfigure:$springBootVersion"

  testCompile "org.spockframework:spock-core:0.7-groovy-2.0"

  runtime("com.h2database:h2:1.3.173")
}

if(System.getProperty("debug.ratpack")) {
  def configureRun = project.tasks.findByName("configureRun")
  configureRun.doLast {
    JavaExec runTask = project.tasks.findByName("run") as JavaExec
    runTask.with {
      jvmArgs (['-Xdebug', '-Xnoagent', '-Djava.compiler=NONE',
               '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'] + runTask.jvmArgs)
    }
  }
} else {
  dependencies {
    // SpringLoaded enables runtime hot reloading.
    // It is not part of the app runtime and is not shipped in the distribution.
    springloaded "org.springframework:springloaded:1.2.0.RELEASE"
  }
}
